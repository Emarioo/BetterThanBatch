/*
    This script prepares a release of the compiler.

    The release contains these things:
    - Executable
    - Docs
    - Examples
    - Libs (C libs)
    - Modules

    We prepare a release for Windows and Linux
*/

#import "File"
#import "Array"
#import "Logger"
#import "String"
#import "OS"

fn main() {

    // ###############
    //      Config
    // #################

    // btb-win-x64-2024-06
    // btb-linux-x64-2024-06a // a = revision

    path_release: StringBuilder
    path_release.append("bin/btb-0.2.0.1")
    #if OS_WINDOWS
        path_release.append("-win_x64")
        path_exe        := "bin/btb.exe"
    #else
        path_release.append("-linux_x64")
        path_exe        := "bin/btb"
    #endif
    path_bin := path_release.copy()
    path_bin.append("-bin")

    path_docs       := "docs"
    path_examples   := "examples"
    path_libs       := "libs"
    path_modules    := "modules"

    // #################
    //      Code
    // #################


    // compile compiler

    // log(path_release)

    DirectoryCreate(path_release.sliced())

    cmd: StringBuilder
    #if OS_WINDOWS
        cmd.append("python build.py use_optimizations use_debug use_tracy=false output=")
        cmd.append(path_release)
        cmd.append("/btb.exe msvc")
        cmd.append(" bin_dir=")
        cmd.append(path_bin)
    #else
        cmd.append("python3 build.py use_optimizations use_debug use_tracy=false output=")
        cmd.append(path_release)
        cmd.append("/btb gcc")
        cmd.append(" bin_dir=")
        cmd.append(path_bin)
    #endif
    // log(cmd)
    ExecuteCommand(cmd.sliced(), null, false)

    #macro COPY_DIR(path)
    {
        tmp := path_release.copy()
        tmp.append("/" ## path)
        DirectoryCopy(path,tmp.sliced());
    }
    #endmacro

    // COPY_DIR("docs")
    // COPY_DIR("examples")
    // COPY_DIR("assets") // not strictly necessary but there are some examples that rely on fonts
    COPY_DIR("modules")
    COPY_DIR("libs")

    readme := @strbeg
Hello curious programmer!

This version 0.2.0 of the BTB compiler. Read guide and examples on `www.github.com/Emarioo/BetterThanBatch` to begin using the language!

**DISCLAIMER**: This version is not very stable. You will most likely run in to bugs and problems. You can report any bugs you find on the github repository `www.github.com/Emarioo/BetterThanBatch`

The standard library is very experimental, functions for the operating system does not handle errors very well.

The compiler is in early stages and will keep changing a lot until the 1.0.0 release.
One of the planned breaking changes is a redo of the preprocessor and macros.
    @strend

    {
        tmp := path_release.copy()
        tmp.append("/README.md")
        // log(tmp)
        file := FileOpen(tmp.sliced(), FILE_CLEAR_AND_WRITE, null)
        // log(name_of_error(HandleToInternal(file)))
        err := FileWrite(file, readme.ptr, readme.len)
        // log(name_of_error(err))
        FileClose(file)
    }
    
    log("Prepared release in ",path_release)
}