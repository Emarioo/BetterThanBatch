// import allocation functions
#import "OS"
#import "Logger"

#macro ARRAY_CLEANUP(ARR) for @ptr ARR.sliced_unsafe() it.cleanup(); ARR.cleanup();

// TODO: Asserts
// Note that memory is uninitialized
struct DynamicArray<T> {
    // zero initialized by default
    ptr: T*;
    max: u32; // allocated bytes
    len: u32; // elements in allocation

    fn cleanup() { reserve(0); }

    // returns index where element was inserted
    // fn add(newElement: T) -> u32 {
    fn add(newElement: T) -> T* {
        if len + 1u > max {
            res: bool = reserve(7u + max * 2u);
            // res: bool = reserve(1 + max); // nocheckin, small growth when debugging
            if !res {
                return null;
            }
        }
        ptr[len] = newElement;
        ptr_elem := ptr + len * sizeof T;
        // ind = len;
        len++;
        return ptr_elem;
        // return ind;
    }
    fn size() -> u32 { return len; }
    fn sliced_unsafe() -> Slice<T> {
        return Slice<T>{ptr, len};
    }
    fn pop() {
        if len > 0u
            len--
    }
    fn remove(index: u32) {
        if index >= len
            return;
        for index..len-1 {
            ptr[nr] = ptr[nr+1];
        }
        // TODO: Cleanup?
        ptr[len-1] = T{};
        len--;
    }
    fn get(index: u32) -> T {
        if(index >= len)
            return T{}; // TODO: Assert instead
        return ptr[index];
    }
    fn get_unsafe(index: u32) -> T* {
        if(index >= len)
            return null;
        return ptr + index * sizeof T;
    }
    fn resize(newLen: u32) -> bool {
        if newLen > max {
            if !reserve(newLen)
                return false;
        }
        len = newLen;
        return true;
    }
    fn reserve(newMax: u32) -> bool {
        // log("newMax",newMax)
        // log("size",sizeof T)
        if newMax==0 {
            // log("{",cast<u64>ptr, max, len,"}",newMax);
            if max!=0 {
                // log("oh")
                Free(ptr, max * sizeof T);
                // log("nah")
            }
            ptr = null;
            max = 0;
            len = 0;
            return true;
        }
        // log("what",cast<u64>ptr)
        if !ptr {
            // log("huh")
            // log("Try reserve, sizeof T =",sizeof T)
            ptr = Allocate(newMax * sizeof T);
            memzero(ptr, newMax * sizeof T)
            // log("allocSize",newMax * sizeof T)
            // log("ptr",ptr)
            // Assert(ptr);
            // initialization of elements is done when adding them
            if !ptr
                return false;
            len = 0;
            max = newMax;
            return true;
        } else {
            ns := newMax * sizeof T;
            os := max * sizeof T;
            newPtr: T* = Reallocate(ptr, os, ns);
            // Assert(newPtr);
            if !newPtr
                return false;
            if newMax > max
                memzero(newPtr + len * sizeof T, (newMax - max) * sizeof T);
            ptr = newPtr;
            max = newMax;
            if len > newMax {
                len = newMax;
            }
            return true;
        }
        return false;
    }
}

// Some code to quickly test an array
// #import "Logger"
// #import "Array"
// arr: DynamicArray<i32>;
// arr.add(4)
// arr.add(23)
// arr.pop()
// arr.add(92)
// arr.add(92)
// arr.add(92)
// for arr.sliced() {
//     log(it)
// }
// log(arr.len, arr.max)
// arr._reserve(0);