#import "Logger"

#import "Array"
#import "Stream"
#import "Lexer"


fn main(argc: i32, argv: char**) -> i32 {
    // arguments
    // for 0..argc
    //     log(nr,argv[nr])
    TestSpec(); 
    
    // LexFile("examples/binary_viewer/spec.txt")

    // log(sizeof TypeDefinition)

    return 2;
}

fn TestSpec() {
    suc: bool;
    spec: FormatSpec;
    stream: ByteStream;
    defer {
        spec.cleanup();
        stream.cleanup();
    }

    // // Format description
    // spec.init();
    // type_header := spec.createDefinition("Header")
    // spec.addField("magic", TYPE_I32, type_header)
    // spec.addField("section_count", TYPE_I32, type_header)
    // spec.setBaseField(type_header)

    // // Test data
    // stream.reserve(0x1000);
    // suc = stream.write<i32>(5);
    // suc = stream.write<i32>(0x9932949);
    // suc = stream.write<i32>(92);
    // print_bytes(&stream);
    // Interpret(stream.sliced_unsafe(), &spec);
    
    suc = ParseSpecification("examples/binary_viewer/spec.txt", &spec);
    Interpret("examples/binary_viewer/data.txt", &spec);
}

enum FieldType {
    TYPE_NONE=0, // usually represents the fields in the specification itself
    TYPE_CHAR,
    TYPE_I8,
    TYPE_I16,
    TYPE_I32,
    TYPE_I64,
    TYPE_U8,
    TYPE_U16,
    TYPE_U32,
    TYPE_U64,
    TYPE_F32,
    TYPE_F64,

    TYPE_PRIMITIVE_COUNT,
}
struct Field {
    type: u32; // user defined struct or primitive
    count: u32;
    dynamic: bool; // dynamic based on another field, could be an expression?
    // value: u32; // index to the value data, stored in spec struct
    name: char[];
}
struct TypeDefinition {
    type: u32; // skip this member, index to access the type definition is the same as this value
    size: u32;
    name: char[];

    fields: Array<Field>;
}
struct FormatSpec {
    base_field: u32; // FieldType or reference to a TypeDefinition 
    definitions: Array<TypeDefinition>; // TODO: BucketArray

    fn init() {
        #macro ADD(...) definitions.add(TypeDefinition{TYPE_##...});
        ADD(NONE, 0, "none")
        ADD(CHAR, 1, "char")
        
        ADD(I8  , 1, "i8")
        ADD(I16 , 2, "i16")
        ADD(I32 , 4, "i32")
        ADD(I64 , 8, "i64")

        ADD(U8  , 1, "u8")
        ADD(U16 , 2, "u16")
        ADD(U32 , 4, "u32")
        ADD(U64 , 8, "u64")

        ADD(F32 , 4, "f32")
        ADD(F64 , 8, "f64")
        #undef ADD
    }
    fn cleanup() {
        base_field = 0;
        for @ptr definitions.sliced_unsafe()
            it.fields.cleanup();
        definitions.cleanup();
    }
    fn setBaseField(type: u32) {
        base_field = type;
    }
    fn getDefinition(definition: u32) -> TypeDefinition* {
        return definitions.get_unsafe(definition);
    }
    fn createDefinition(name: char[]) -> u32 {
        def := definitions.add(TypeDefinition{});
        def.type = definitions.size() - 1;
        def.name = name;
        return def.type;
    }
    fn getField(definition: u32, fieldIndex: u32) -> Field* {
        // get_unsafe handles out of range
        def := getDefinition(definition);
        return def.fields.get_unsafe(fieldIndex);
    }
    // returns index in TypeDefinition.fields (or FormatSpec) where field was placed
    fn addField(name: char[], type: u32, definition: u32) -> u32 {
        def := getDefinition(definition);
        if def {
            f := def.fields.add(Field{type});
            f.name = name;
            f.count = 1;
            typeDef := getDefinition(type);
            def.size += typeDef.size;
            return def.fields.size() - 1;
        } else {
            std_prints("BAD, '",definition,"' is not a definition")
        }
        return -1;
    }
    // return 0 if string isn't a type
    fn typeFromString(string: char[]) -> u32 {
        for @ptr definitions.sliced_unsafe() {
            if it.name == string {
                return it.type;
            }
        }
        return 0;
    }
}

fn Interpret(path: char[], spec: FormatSpec*) {
    filesize: u64;
    file := FileOpen(&path, FILE_READ_ONLY, &filesize);
    if !file
        return;
        
    suc: bool;
    buffer: u8* = Allocate(filesize);
    defer Free(buffer, filesize);
    suc = FileRead(file, buffer, filesize);
    // TODO: Assert(suc)
    FileClose(file);
    
    Interpret(Slice<u8>{buffer, filesize}, spec);
}

// Prints the content of the stream based on the specification.
fn Interpret(stream: u8[], spec: FormatSpec*) {
    // TODO: The output should go through a standard logger with color and redirection support.
    //  Perhaps it's written to stdout, a file, and a StringBuilder.

    // Assert(spec)

    head: u32 = 0; // read head of stream

    struct Layer {
        count: u32 = 0;
        type: u32 = 0; // 0 refers to specification
        fieldIndex: u32 = 0;
    }
    stack: Array<Layer>;
    stack.add(Layer{type = spec.base_field, fieldIndex = 0});

    std_prints("Interpret stream of ",stream.len," bytes\n")

    fn print_indent(indent: i32) {
        for 0..indent  printc(' ')
    }
    
    while stack.len {
        // get next field
        layer := stack.get_unsafe(stack.len - 1);

        element: Field* = spec.getField(layer.type, layer.fieldIndex);
        if !element {
            layer.count = 0;
            // log("pop",layer.type);
            stack.pop();
            continue;
        }
        elemDef := spec.getDefinition(element.type);
        layer.count++;
        defer if element.count == layer.count {
            layer.fieldIndex++;
            layer.count = 0;
        }

        print_indent(stack.len-1);
        
        // Assert(element.name.ptr)
        if element.count == 1 {
            std_prints(element.name,": ");
        } else {
            if layer.count == 1 {
                std_prints(element.name);
            } else {
                print_indent(element.name.len)
            }
            std_prints(" [",layer.count - 1,"]: ")
        }
        
        if element.type >= TYPE_PRIMITIVE_COUNT {
            // user defined field. make new stack
            // log("push",element.type);
            stack.add(Layer{element.type, 0});
        } else {
            if head + elemDef.size > stream.len {
                std_prints("<ABRUPT END>\n");
                // corruption somewhere
                break;
            }
            #macro CASE(E,T)
            case TYPE_##E: {
                tmp: T;
                memcpy(&tmp, stream.ptr + head, sizeof T);
                head += sizeof T;
                std_print(tmp);
            }
            #endmacro
            switch element.type {
            case TYPE_CHAR: {
                tmp: char;
                memcpy(&tmp, stream.ptr + head, sizeof char);
                head += sizeof char;
                if tmp < 32u {
                    if tmp == '\n' {
                        std_print("\\n")
                    } else if tmp == '\r' {
                        std_print("\\r")
                    } else if tmp == '\0' {
                        std_print("\\0")
                    } else if tmp == '\t' {
                        std_print("\\t")
                    } else {
                        std_prints("<",cast<u8>tmp,">")
                    }
                } else {
                    std_print(tmp);
                }
            }
            CASE(CHAR, char)

            CASE(I8, i8)
            CASE(I16, i16)
            CASE(I32, i32)
            CASE(I64, i64)

            CASE(U8, u8)
            CASE(U16, u16)
            CASE(U32, u32)
            CASE(U64, u64)

            CASE(F32, f32)
            CASE(F64, f64)
            case: {
                std_prints("UNREACHABLE_312")
            }
            }
                
        }
        std_prints(" (",elemDef.name,")");
        std_prints('\n');
    }
    std_prints("FINISHED, ",head," bytes (",stream.len-head," unread)\n");
}


fn ParseSpecification(path: Slice<char>, out_spec: FormatSpec*) -> bool {
    out_spec.cleanup();
    out_spec.init();
    
    tokenStream := LexFile(path)
    defer DestroyTokenStream(tokenStream);
    if !tokenStream
        return false;
    
    parser: SpecParser;
    parser.tokens = tokenStream;
    parser.spec = out_spec;
    
    tokenStream.print();
    
    parser.parseBody();
    
    header_type := out_spec.typeFromString("Header");
    out_spec.setBaseField(header_type); // add to base fields
    
    tokenStream.rawData = ByteStream{}; // MEMORY LEAK, we "detach" allocation with strings so that the string slices in FormatSpec doesn't refer to deallocated memory.
    // TODO: In the future, FormatSpec should allocate it's own array per field and type name
    
    return true;
}
struct SpecParser {
    tokens: TokenStream*;
    spec: FormatSpec*;
    head: u32; // token index
    
    // TODO: A stack of current types allowing structs within structs
    current_struct_type: u32; // type index/definition in FormatSpec
    
    fn get(off: u32 = 0) -> Token* {
        return tokens.get(head + off);
    }
    fn consume(n: u32 = 1) {
        head += n;
    }
    
#macro PARSE_ERROR(T,...) std_prints("BAD SYNTAX: ") tokens.print(T); std_prints("\n")
    
    fn parseBody() {
        
        while head < tokens.size() {
            tok := get();
            
            if tok.type == TOKEN_STRUCT {
                consume();
                parseStruct();
                continue;
            }
            
            consume();
            PARSE_ERROR(tok)
        }
    }
    fn parseStruct() {
        tok_a := get();
         
        // struct should have been consumed
        
        if tok_a.type == TOKEN_IDENTIFIER {
            slice := tokens.get_string(head);
            consume(); // struct name
            
            // current_struct_type could be a local variable instead of a member.
            // you wouldn't need a stack/list if structs exist within structs, the current_type is a local variable on the stack
            current_struct_type = spec.createDefinition(slice);
        }
        defer current_struct_type = 0;
        
        tok_a = get();
        if tok_a.type == '{' {
            consume();
        } else {
            // handle error
            PARSE_ERROR(tok_a)
        }
        
        while head < tokens.size() {
            tok := get();
            
            if tok.type == '}' {
                consume();
                break;
            }
            
            if tok.type != TOKEN_IDENTIFIER {
                consume(); // prevent infinite loop by consuming
                
                PARSE_ERROR(tok)
                continue;
            }
            member_name := tokens.get_string(head);
            consume();
            
            tok = get();
            
            if tok.type != ':' {
                consume();   
                
                PARSE_ERROR(tok)
                continue;
            }
            consume();
            
            tok = get();
            type_string := tokens.get_string(head);
            
            // TODO: Check for brackets: u32[123]
            
            field_type := spec.typeFromString(type_string);
            field_index := -1;
            if field_type == 0 {
                std_prints("Not a type: ",type_string,"\n");
            } else {
                field_index = spec.addField(member_name, field_type, current_struct_type);
                
            }
            consume();

            tok = get();
            if tok.type == '[' {
                consume();
                
                tok = get();
                if tok.type == TOKEN_LIT_NUMBER {
                    num := tokens.get_number(head);
                    consume();
                    
                    // Assert(field_index != -1)
                    field := spec.getField(current_struct_type, field_index);
                    // Assert(num >= 0)
                    field.count = num;
                    
                    // log("ignoring",num);
                } else {
                    PARSE_ERROR(tok);
                }
                
                tok = get();
                if tok.type == ']' {
                    consume();
                    
                } else {
                    PARSE_ERROR(tok)   
                    // consume();
                }
            }
            
            tok = get();
            if tok.type == ';' { // optional for now, ; is a good marker for the end of the type so maybe bring back later?
                consume();
                
                // PARSE_ERROR(tok)
                // continue;
            }
            
            // Yay!
        }
    }
#undef PARSE_ERROR
}